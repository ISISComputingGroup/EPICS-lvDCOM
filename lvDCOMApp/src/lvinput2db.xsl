<?xml version="1.0" encoding="UTF-8"?>
<!--
    ########### SVN repository information ###################
    # $LastChangedDate$
    # $LastChangedBy$
    # $LastChangedRevision$
    # $HeadURL$
    ########### SVN repository information ###################

    Usage: xsltproc lvinput2db.xsl lvexportstrings.xml > epics_records.db

	@author Freddie Akeroyd, STFC ISIS Facility, UK
	
-->
<xsl:stylesheet
    version="1.0"
    xmlns:xs="http://www.w3.org/2001/XMLSchema"
    xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
	xmlns:lvdcom="http://epics.isis.rl.ac.uk/lvDCOMinput/1.0">
    
    <xsl:output method="text" indent="yes" />

   <xsl:template match="/lvdcom:lvinput">
      <xsl:text># Generated by $Id$</xsl:text>
      <xsl:apply-templates select="lvdcom:section" />
	</xsl:template>
	
   <xsl:template match="lvdcom:section">
      <xsl:variable name="section_name" select="@name" />
      <xsl:apply-templates select="lvdcom:vi" />
	</xsl:template>

   <xsl:template match="lvdcom:vi">
      <xsl:apply-templates select="lvdcom:param" />
   </xsl:template>
   
   <xsl:template match="lvdcom:param">
      <xsl:variable name="asyn_param" select="@name" />
      <xsl:variable name="asyn_type">
	  <xsl:call-template name="convertToAsynType">
	  <xsl:with-param name="vartype" select="@type" />
	  </xsl:call-template>
	</xsl:variable>
	<xsl:choose>
	   <xsl:when test="@type = 'string'">
record(stringin, "$(P)<xsl:value-of select="$asyn_param"/>_RBV")
{
    field(DTYP, "<xsl:value-of select="$asyn_type"/>Read")
    field(INP,  "@asyn(ex1,0,0)<xsl:value-of select="$asyn_param"/>")
    field(SCAN, ".1 second")
}

record(stringout, "$(P)<xsl:value-of select="$asyn_param"/>")
{
    field(DTYP, "<xsl:value-of select="$asyn_type"/>Write")
    field(OUT,  "@asyn(ex1,0,0)<xsl:value-of select="$asyn_param"/>")
}

</xsl:when>	
	    <xsl:when test="@type = 'int32'">
	        record(longin, "$(P)<xsl:value-of select="$asyn_param"/>_RBV")
	        {
	        field(DTYP, "<xsl:value-of select="$asyn_type"/>Read")
	        field(INP,  "@asyn(ex1,0,0)<xsl:value-of select="$asyn_param"/>")
	        field(SCAN, ".1 second")
	        }
	        
	        record(longout, "$(P)<xsl:value-of select="$asyn_param"/>")
	        {
	        field(DTYP, "<xsl:value-of select="$asyn_type"/>Write")
	        field(OUT,  "@asyn(ex1,0,0)<xsl:value-of select="$asyn_param"/>")
	        }
	        </xsl:when>
	    <xsl:when test="@type = 'float64'">
record(ai, "$(P)<xsl:value-of select="$asyn_param"/>_RBV")
{
    field(DTYP, "<xsl:value-of select="$asyn_type"/>")
    field(INP,  "@asyn(ex1,0,0)<xsl:value-of select="$asyn_param"/>")
    field(PREC, "3")
    field(SCAN, ".1 second")
}

record(ao, "$(P)<xsl:value-of select="$asyn_param"/>")
{
    field(DTYP, "<xsl:value-of select="$asyn_type"/>")
    field(OUT,  "@asyn(ex1,0,0)<xsl:value-of select="$asyn_param"/>")
    field(PREC, "3")
}
</xsl:when>
	    <xsl:when test="@type = 'boolean'">
	        record(bi, "$(P)<xsl:value-of select="$asyn_param"/>_RBV")
	        {
	        field(DTYP, "<xsl:value-of select="$asyn_type"/>")
	        field(INP,  "@asyn(ex1,0,0)<xsl:value-of select="$asyn_param"/>")
	        field(SCAN, ".1 second")
	        field(ZNAM, "<xsl:value-of select="lvdcom:items/lvdcom:item[@value=0]/@name"/>")
	        field(ONAM, "<xsl:value-of select="lvdcom:items/lvdcom:item[@value=1]/@name"/>")
	        }
	        
	        record(bo, "$(P)<xsl:value-of select="$asyn_param"/>")
	        {
	        field(DTYP, "<xsl:value-of select="$asyn_type"/>")
	        field(OUT,  "@asyn(ex1,0,0)<xsl:value-of select="$asyn_param"/>")
	        field(ZNAM, "<xsl:value-of select="lvdcom:items/lvdcom:item[@value=0]/@name"/>")
	        field(ONAM, "<xsl:value-of select="lvdcom:items/lvdcom:item[@value=1]/@name"/>")
	        }
	        
	    </xsl:when>
	    <xsl:when test="@type = 'ring' or @type = 'enum'">
	        record(mbbi, "$(P)<xsl:value-of select="$asyn_param"/>_RBV")
	        {
	        field(DTYP, "<xsl:value-of select="$asyn_type"/>")
	        field(INP,  "@asyn(ex1,0,0)<xsl:value-of select="$asyn_param"/>")
	        field(SCAN, ".1 second")
	        <xsl:call-template name="allmb" />
	        }
	        
	        record(mbbo, "$(P)<xsl:value-of select="$asyn_param"/>")
	        {
	        field(DTYP, "<xsl:value-of select="$asyn_type"/>")
	        field(OUT,  "@asyn(ex1,0,0)<xsl:value-of select="$asyn_param"/>")
	        <xsl:call-template name="allmb" />
	        }
	        
	    </xsl:when>
	    <xsl:otherwise>
#
# ERROR type <xsl:value-of select="@type"/> is invalid 
#
</xsl:otherwise>
	</xsl:choose>
  </xsl:template>

    <xsl:template name="allmb">
       <xsl:call-template name="mb"><xsl:with-param name="prefix">ZR</xsl:with-param><xsl:with-param name="pos">1</xsl:with-param></xsl:call-template>
        <xsl:call-template name="mb"><xsl:with-param name="prefix">ON</xsl:with-param><xsl:with-param name="pos">2</xsl:with-param></xsl:call-template>
        <xsl:call-template name="mb"><xsl:with-param name="prefix">TW</xsl:with-param><xsl:with-param name="pos">3</xsl:with-param></xsl:call-template>
        <xsl:call-template name="mb"><xsl:with-param name="prefix">TH</xsl:with-param><xsl:with-param name="pos">4</xsl:with-param></xsl:call-template>
        <xsl:call-template name="mb"><xsl:with-param name="prefix">FR</xsl:with-param><xsl:with-param name="pos">5</xsl:with-param></xsl:call-template>
    </xsl:template>

    <xsl:template name="mb">
        <xsl:param name="prefix" />
        <xsl:param name="pos" />
        <xsl:variable name="node" select="lvdcom:items/lvdcom:item[position()=$pos]" />
        <xsl:if test="$node">
            field(<xsl:value-of select="$prefix"/>VL, <xsl:value-of select="$node/@value"/>)
        field(<xsl:value-of select="$prefix"/>ST, "<xsl:value-of select="$node/@name"/>")
        </xsl:if>
    </xsl:template>

    <xsl:template name="convertToAsynType">
	<xsl:param name="vartype" />
	<xsl:choose>
	    <xsl:when test="$vartype = 'int32'">asynInt32</xsl:when>		
	    <xsl:when test="$vartype = 'enum'">asynInt32</xsl:when>		
	    <xsl:when test="$vartype = 'ring'">asynInt32</xsl:when>		
	    <xsl:when test="$vartype = 'boolean'">asynInt32</xsl:when>		
	    <xsl:when test="$vartype = 'float64'">asynFloat64</xsl:when>		
	   <xsl:when test="$vartype = 'string'">asynOctet</xsl:when>		
		<xsl:otherwise>invalid</xsl:otherwise>
	</xsl:choose>
	
</xsl:template>

</xsl:stylesheet>
<!-- asynFloat64ArrayIn  -->
<!--
   /CONTENT/CONTROL/@type    Numeric(ID=80)  String(ID=81)  Array(ID=82) Boolean(ID=79)    Cluster    "Radio Buttons" "Ring" "Listbox" "Enum" "Type Definition"
	/CONTENT/CONTROL/@name
	
	if array, /CONTENT/CONTROL/CONTENT/CONTROL/@type  Numeric
-->
